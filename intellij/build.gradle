allprojects {
	apply plugin: 'maven'
	group = 'org.eclipse.xtext'
	version = '2.8.0-SNAPSHOT'
	
	repositories {
		if (!System.getProperty("JOB_NAME")) {
			mavenLocal()
		}
		mavenCentral()
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
}

subprojects {
	apply plugin: 'org.xtext.idea-dev'
	apply from: "${rootDir}/gradle/eclipse-project-layout.gradle"
	
	configurations.compile {
		exclude module:'guava'
		exclude module:'cglib'
		exclude module:'log4j'
		exclude module:'icu4j'
		exclude group:'org.ow2.asm'
		exclude module:'org.eclipse.jdt.core'
	}
	
	sourceCompatibility = 1.6
	
	ideaDevelopment {
		ideaHome = "${rootDir}/intellij-ce"
	}
}

def intellijPluginProjects = [
	project(':org.eclipse.xtext.idea'),
	project(':org.eclipse.xtext.xtext.idea'),
	project(':org.eclipse.xtext.xbase.idea'),
	project(':org.eclipse.xtext.idea.sdomain.idea'),
	project(':org.eclipse.xtext.idea.example.entities.idea'),
	project(':org.eclipse.xtend.idea')
]

configure(intellijPluginProjects) {
	apply plugin: 'org.xtext.idea-plugin'
	task zip(dependsOn: jar, type: Zip) {
		from configurations.runtime.allArtifacts.files
		from configurations.runtime - configurations.ideaProvided
		into(project.name + '-' + project.version + '/lib')
	}
}

//TODO generate this automatically
task createIntellijRepository(type: Copy) {
	from "updatePlugins.xml"
	from intellijPluginProjects*.zip
	into "$rootDir/build/intellijRepository"
}