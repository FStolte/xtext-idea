buildscript {
	dependencies {
		classpath "org.xtext:xtext-idea-gradle-plugin:0.3.7"
	}
}

def ideaTestProjects = [
	project('org.eclipse.xtext.xbase.idea.tests'),
	project('org.eclipse.xtext.core.idea.tests'),
	project('org.eclipse.xtext.idea.sdomain.idea.tests'),
	project('org.eclipse.xtend.idea.tests'),
	project('org.eclipse.xtext.idea.common.types.tests')
]

def ideaPluginProjects = [
	project('org.eclipse.xtext.idea'),
	project('org.eclipse.xtext.xtext.idea'),
	project('org.eclipse.xtext.xbase.idea'),
	project('org.eclipse.xtext.idea.sdomain.idea'),
	project('org.eclipse.xtext.idea.example.entities.idea'),
	project('org.eclipse.xtend.idea')
]

apply plugin: 'org.xtext.idea-repository'
apply plugin: 'org.xtext.idea-aggregator'
ideaRepository.rootUrl = 'https://hudson.eclipse.org/xtext/job/xtext-intellij/lastSuccessfulBuild/artifact/git-repo/intellij'

allprojects {
	apply plugin: 'org.xtext.idea-dev'
	
	ideaDevelopment {
		ideaHome = "${project(':intellij').projectDir}/intellij-ce"
		ideaVersion = "140.2683.2"
	}
	
	runIdea.systemProperties ('compiler.process.debug.port':-1) // replace -1 by port number to enable debugging
}

subprojects {
	apply plugin: 'org.xtext.idea-component'
	
	configurations.compile {
		exclude module:'cglib'
		exclude module:'icu4j'
		exclude group:'org.ow2.asm'
		exclude module:'org.eclipse.jdt.core'
	}
}

configure(ideaTestProjects) {
	sourceSets.test.java.srcDirs ('src', 'src-gen', 'xtend-gen', 'tests')
	sourceSets.main.java.srcDirs = []
	dependencies {
		testCompile project(':intellij:org.eclipse.xtext.idea.tests')
	}
	test {
		minHeapSize = "1024m"
		jvmArgs '-XX:MaxPermSize=256m'
		exclude {it.name.endsWith('Delegate.class')}
	}
}

configure(ideaPluginProjects) {
	apply plugin: 'org.xtext.idea-plugin'
}
