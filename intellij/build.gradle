buildscript {
	dependencies {
		classpath "org.xtext:xtext-idea-gradle-plugin:0.3.10"
	}
}

def eclipsePluginProjects = subprojects.findAll{it.file('build.properties').exists()}
def ideaPluginProjects = subprojects.findAll{it.file('META-INF/plugin.xml').exists() && ! it.name.endsWith('tests')}
def ideaTestProjects = subprojects.findAll{it.name.endsWith('idea.tests')} + project('org.eclipse.xtext.idea.common.types.tests') - project('org.eclipse.xtext.idea.tests')

apply plugin: 'org.xtext.idea-repository'
apply plugin: 'org.xtext.idea-aggregator'
ideaRepository.rootUrl = 'https://hudson.eclipse.org/xtext/job/xtext-intellij/lastSuccessfulBuild/artifact/git-repo/'

allprojects {
	apply plugin: 'org.xtext.idea-dev'
	
	ideaDevelopment {
		ideaHome = "${project(':intellij').projectDir}/intellij-ce"
		ideaVersion = "140.2683.2"
	}
}

configure(eclipsePluginProjects) {
	apply from: "${rootDir}/gradle/eclipse-plugin-project.gradle"
}

configure(subprojects - eclipsePluginProjects) {
	apply plugin: 'org.xtext.idea-component'
	apply from: "$rootDir/gradle/eclipse-classpath-fix.gradle"
	
	configurations.compile {
		exclude module:'cglib'
		exclude module:'icu4j'
		exclude group:'org.ow2.asm'
		exclude module:'org.eclipse.jdt.core'
	}
}

configure(ideaPluginProjects) {
	apply plugin: 'org.xtext.idea-plugin'
}

configure(ideaTestProjects) {
	sourceSets.test.java.srcDirs ('src', 'src-gen', 'xtend-gen', 'tests')
	sourceSets.main.java.srcDirs = []
	dependencies {
		testCompile project(':intellij:org.eclipse.xtext.idea.tests')
	}
	test {
		minHeapSize = "1024m"
		jvmArgs '-XX:MaxPermSize=256m'
		exclude {it.name.endsWith('Delegate.class')}
	}
}