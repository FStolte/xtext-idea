/*
 * Root project configuration that is reused by subprojects to apply the Xtend compiler.
 */

// The repositories to query when constructing the Xtend compiler classpath
repositories {
	jcenter()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

// The Xtend compiler version to use
def bootstrapXtendVersion = versions.xtext

configurations {
	xtendCompiler {
		description 'Bootstrap dependencies for the Xtend compiler'
		resolutionStrategy {
			eachDependency {
				if (requested.group == 'org.eclipse.xtext' || requested.group == 'org.eclipse.xtend')
					useVersion(bootstrapXtendVersion)
				if (requested.group == 'com.google.inject' && requested.name == 'guice')
					useVersion(versions.guice)
			}
		}
		exclude group: 'asm'
	}
}

dependencies {
	xtendCompiler "org.eclipse.xtend:org.eclipse.xtend.core:$bootstrapXtendVersion"
	xtendCompiler "org.xtext:xtext-gradle-builder:$versions.xtend_plugin"
}
